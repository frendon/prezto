#
# A simple theme that displays relevant, contextual information.
#
# Authors:
#   frendon Ionescu <sorin.ionescu@gmail.com>
#
#

# Load dependencies.
pmodload 'helper'

function prompt_frendon_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_frendon_pwd="$MATCH"
    unset MATCH
  else
    _prompt_frendon_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_frendon_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_frendon_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_frendon_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_frendon_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format ''
  zstyle ':prezto:module:editor:info:keymap:primary' format ''
  # zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}♺%f'
  # zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{green}❮%F{yellow}❮%F{red}❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format '%F{green}%b%f'
  zstyle ':prezto:module:git:info:clean' format '%F{green}✔%b%f'
  zstyle ':prezto:module:git:info:dirty' format '%%B%F{yellow}✗%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' ' %F{blue}(%f$(coalesce "%b" "%p" "%c")${git_info[rprompt]}%s%F{green}%f%F{blue})%f%s' \
    'rprompt' '%C%D'
    #'rprompt' '%A%B%S%a%d%m%r%U%u'

  # Define prompts.
  PROMPT='%(?:%B%F{green}➜%f%b:%F{red}➜%f%b)  %F{cyan}${_prompt_frendon_pwd}%f${git_info:+${(e)git_info[prompt]}}${editor_info[keymap]} '
  # RPROMPT='${editor_info[overwrite]}%(?:: %F{red}⏎%f)${VIM:+" %B%F{green}V%f%b"}${git_info[rprompt]}'
  RPROMPT=''
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_frendon_setup "$@"
